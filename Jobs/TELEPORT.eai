#IFDEF GLOBAL
  boolean teleporting = false
  location teleportloc = Location(0,0)
#ELSE
function TeleportJob takes unit hu returns nothing
  local group g = null
  local group tempg = null
  local unit u = null
  local integer current_order = GetUnitCurrentOrder(hu)

  call DisplayToAllJobDebug("TELEPORT JOB Start")
  if not UnitAlive(hu) or current_order < 852008 or current_order > 852013 then
    if not UnitAlive(hu) then
      call CaptainGoHome()
    endif
    set teleporting = false
    return
  endif
  set g = CreateGroup()
  set tempg = CreateGroup()
  call GroupEnumUnitsInRange(g, GetUnitX(hu), GetUnitY(hu), 2 * teleport_radius, null)
  call GroupRemoveUnit(g, hu)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if not IsUnitType(u, UNIT_TYPE_PEON) and GetOwningPlayer(u) == ai_player and not IsUnitHidden(u) then
      if DistanceBetweenUnits(hu, u) > teleport_radius then
        call IssuePointOrder(u, "move", GetUnitX(hu), GetUnitY(hu))
        call GroupAddUnit(tempg,u)
      endif
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call GroupClear(g)
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  call BlzGroupRemoveGroupFast(tempg,g)
  call DestroyGroup(tempg)
  set tempg = null
  set g = SelectByHidden(g, false)
  set g = SelectUnittype(g, UNIT_TYPE_PEON, false)
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, false)
  set g = SelectByUnitFree(g, true)
  set g = SelectByUnitStandard(g, true)
  call GroupPointOrder(g, "move", GetLocationX(teleportloc), GetLocationY(teleportloc))
  call DestroyGroup(g)
  set g = null
  call TQAddUnitJob(1, TELEPORT, 0, hu)
endfunction
#ENDIF