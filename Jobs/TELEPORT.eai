#IFDEF GLOBAL
  boolean teleporting = false
  location teleportloc = null
#ELSE
function TeleportJob takes unit hu returns nothing
  local group g = null
  local group tempg = null
  local unit u = null
  local integer current_order = GetUnitCurrentOrder(hu)

  call DisplayToAllJobDebug("TELEPORT JOB Start")
  if not UnitAlive(hu) or current_order < 852008 or current_order > 852013 then
    call CaptainGoHome()
    set teleporting = false
    return
  endif
  set g = CreateGroup()
  call GroupEnumUnitsInRange(g, GetUnitX(hu), GetUnitY(hu), 2 * teleport_radius, null)
  call GroupRemoveUnit(g, hu)
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_PEON, false)
  set tempg = CopyGroup(g)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if DistanceBetweenUnits(hu, u) > teleport_radius then
      call IssuePointOrder(u, "move", GetUnitX(hu), GetUnitY(hu))
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call GroupClear(g)
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  call GroupRemoveGroupAM(tempg,g)
  call DestroyGroup(tempg)
  set tempg = null
  set g = SelectUnittype(g, UNIT_TYPE_PEON, false)
  set g = SelectByUnitStandard(g, true)
  set g = SelectByUnitFree(g, true)
  set g = SelectByHidden(g, false)
  call GroupPointOrder(g, "move", GetLocationX(teleportloc), GetLocationY(teleportloc))
  call DestroyGroup(g)
  set g = null
  call TQAddUnitJob(1, TELEPORT, 0, hu)
endfunction
#ENDIF